buildscript {
    dependencies {
        // to fix an issue exposed by spotbugs 2.0.0: https://github.com/spotbugs/spotbugs-gradle-plugin/issues/120
        classpath "com.google.guava:guava:${pluginGuavaVersion}"
    }
}

plugins {
    id "org.owasp.dependencycheck" version "4.0.0.1"
    id "com.github.spotbugs" version "2.0.0"
    id 'pmd'
    id 'checkstyle'
}

apply plugin: 'java-library'

group = 'uk.gov.dhsc.htbhf'
sourceCompatibility = 1.11
targetCompatibility = 1.11

repositories {
    mavenCentral()
    maven {
        url  "https://dl.bintray.com/departmentofhealth-htbhf/maven"
    }
}


dependencies {
    implementation "org.springframework.cloud:spring-cloud-spring-service-connector:2.0.5.RELEASE"
    implementation "org.springframework.cloud:spring-cloud-cloudfoundry-connector:2.0.5.RELEASE"

    spotbugsPlugins "com.h3xstream.findsecbugs:findsecbugs-plugin:${spotbugsFindsecVersion}"
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

checkstyle {
    toolVersion "8.15"
}

pmdMain {
    ignoreFailures = false
    ruleSetFiles = files("../config/pmd/rulesets.xml")
    ruleSets = [] // Remove built-in rule sets
}
pmdTest {
    ignoreFailures = false
    ruleSetFiles = files("../config/pmd/test-rulesets.xml")
    ruleSets = [] // Remove built-in rule sets
}
spotbugsTest {
    excludeFilter = file("../config/spotbugs/test-exclude.xml")
}
javadoc {
    failOnError = true
}
