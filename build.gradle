buildscript {

    ext {
        springVersion = '5.1.10.RELEASE'
        springBootVersion = '2.1.8.RELEASE'
        junitVersion = '5.3.2'
        spotbugsFindsecVersion = '1.9.0'
        pluginGuavaVersion = '27.0.1-jre'
    }

    repositories {
        jcenter()
        mavenCentral()
    }
}

plugins {
    id 'org.shipkit.java' version '2.2.5'
}

allprojects {
    apply plugin: 'jacoco'
    apply plugin: 'idea'

    group = 'uk.gov.dhsc.htbhf'

    repositories {
        jcenter()
    }

    jacoco {
        toolVersion = "0.8.2"
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'jacoco'
    apply plugin: 'maven-publish' // allows us to run gradle publishToMavenLocal

    sourceCompatibility = 1.11
    targetCompatibility = 1.11

    test {
        reports {
            junitXml.enabled = true
            html.enabled = true
        }
    }

    jacocoTestReport {
        additionalSourceDirs.from(files(sourceSets.main.allSource.srcDirs))
        sourceDirectories.from(files(sourceSets.main.allSource.srcDirs))
        classDirectories.from(files(sourceSets.main.output))
        reports {
            html.enabled = true
            xml.enabled = true
            csv.enabled = false
        }
    }

    task updateConfig() {
        'git submodule update --init --recursive --remote'.execute()
    }

    tasks.check.dependsOn updateConfig
}

task jacocoReport(type: org.gradle.testing.jacoco.tasks.JacocoReport) {
    dependsOn = subprojects.test
    additionalSourceDirs.from(files(subprojects.sourceSets.main.allSource.srcDirs))
    sourceDirectories.from(files(subprojects.sourceSets.main.allSource.srcDirs))
    classDirectories.from(files(subprojects.sourceSets.main.output))
    executionData.from(files(subprojects.jacocoTestReport.executionData))
    reports {
        html.enabled = true
        xml.enabled = true
        csv.enabled = false
    }
    onlyIf = {
        true
    }
    doFirst {
        executionData = files(executionData.findAll {
            it.exists()
        })
    }
}

task testReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/tests")
    // Include the results from the `test` task in all subprojects
    reportOn subprojects*.test
}

def testCounter = 0
allprojects {
    tasks.withType(Test) {
        afterSuite { testDescriptor, testResult ->
            testCounter += testResult.testCount
        }
    }
}

task copyCommonRestReports(type: Copy) {
    from(file('common_rest/build/reports'))
    into(file('build/reports'))
    outputs.upToDateWhen { false }
}

task copyAdditionalDocumentation(type: Copy) {
    from(file('src/documentation/gh-pages'))
    into(file('build/reports'))
}

task copyReports() {
    dependsOn copyCommonRestReports, copyAdditionalDocumentation
}
